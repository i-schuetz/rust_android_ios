apply plugin: 'com.android.application'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "com.schuetz.rust_android_ios"
        minSdkVersion 21
        targetSdkVersion 28
        versionCode 1
        versionName "1.0.1"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    sourceSets {
        main {
            jni.srcDirs = []
            jniLibs.srcDir 'src/main/libs'
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    androidTestImplementation('com.android.support.test.espresso:espresso-core:2.2.2', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })
    api 'com.android.support:appcompat-v7:28.0.0'
    api 'com.android.support.constraint:constraint-layout:1.1.3'
    api 'com.android.support:design:28.0.0'
    testImplementation 'junit:junit:4.12'
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"
}

// ANCHOR:cargo
def rustBasePath = ".."
def archTriplets = [
    'armeabi': 'arm-linux-androideabi',
    'arm64': 'aarch64-linux-android',
    'x86_64': 'x86_64-linux-android'
]

// TODO: only pass --release if buildType is release
archTriplets.each { arch, target ->

    // Build Rust lib
    tasks.create(name: "cargo-build-$arch", type: Exec, description: "Building core for $arch") {
            workingDir rustBasePath
            executable "cargo"
            args = ["ndk", "--platform", "29", "--target", target, "build", "--verbose"]
    }

    // Copy Rust lib into this app's libs directory
    tasks.create(name: "rust-deploy-$arch", type: Copy, dependsOn: "cargo-build-$arch",
            description: "Copy rust libs for ($arch) to libs") {

        from "$rustBasePath/target/$target/debug"
        include "*.so"
        into "$rustBasePath/app/src/main/libs/$arch"
    }

    // Hook up tasks to execute before building java
    tasks.withType(JavaCompile) {
        compileTask -> compileTask.dependsOn "rust-deploy-$arch"
    }
}

repositories {
    mavenCentral()
}
// ANCHOR_END:cargo
